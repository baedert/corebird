m4_define([api_major], [0])
m4_define([api_minor], [7])
m4_define([api_micro], [93])

m4_define([rest_version], [api_major.api_minor.api_micro])


AC_PREREQ([2.63])
AC_INIT([rest], [rest_version],
        [],
        [rest],
        [http://git.gnome.org/browse/librest/])

AC_CONFIG_SRCDIR([rest/rest-proxy.h])
AC_CONFIG_HEADERS([config.h])
AC_CONFIG_AUX_DIR([build])
AC_CONFIG_MACRO_DIR([build])

AM_INIT_AUTOMAKE([1.11 foreign -Wno-portability no-define dist-xz])

AM_SILENT_RULES([yes])

API_MAJOR=api_major
API_MINOR=api_minor
AC_SUBST([API_VERSION],[$API_MAJOR.$API_MINOR])
AC_SUBST([API_VERSION_AM],[$API_MAJOR\_$API_MINOR])
AC_DEFINE_UNQUOTED(API_VERSION, [$API_VERSION], [API version])

AC_CANONICAL_HOST

AC_PROG_CC
AC_PROG_CC_STDC
AC_PROG_INSTALL
AC_ISC_POSIX
AC_HEADER_STDC
AM_PROG_AR

AM_PROG_CC_C_O

# require libtool >= 2.2
LT_PREREQ([2.2.6])
LT_INIT

PKG_CHECK_MODULES(GLIB, glib-2.0 >= 2.24)
PKG_CHECK_MODULES(SOUP, libsoup-2.4)
PKG_CHECK_MODULES(GTHREAD, gthread-2.0)

# gtkdocize greps for ^GTK_DOC_CHECK and parses it, so you need to have
# it on it's own line.
m4_ifdef([GTK_DOC_CHECK], [
GTK_DOC_CHECK([1.13], [--flavour no-tmpl])
])

AC_PATH_PROG([GLIB_MKENUMS],[glib-mkenums])

localedir=${datadir}/locale
AC_SUBST(localedir)
AC_OUTPUT([
        Makefile
        rest/Makefile
        rest.pc
])
